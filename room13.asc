// Pizza Shack (von außen) (Syd)

bool b_ShackOffen = false;
/****************************
*   Raum-Funktionen
****************************/
function room_Load()
{
	cJeff.FaceDirection(eDirUp);
	oTuer.init_object(11);
  
  if (dTom.GetOptionState(6) == eOptionOffForever && cEd.Room != 25 && cEd.Room != 7)
  {
    cEd.ChangeRoom(13, 305, 135);
    cEd.FaceDirection(eDirLeft);
    ControllMode(true, false);
    gAction.Visible=false;
  }
  if (player.PreviousRoom != 7) aRoad.Play(eAudioPriorityLow, eRepeat);
}

function room_AfterFadeIn()
{
  aSCUMMBar.Play();
	//PlayAmbientSound(1, 101, 255, 155, 140);
  
  // Syd encounters Ed.
  if (dTom.GetOptionState(6) == eOptionOffForever && cEd.Room != 25 && cEd.Room != 7)
  {
    StartCutscene(eSkipESCOrRightButton);
    Mouse.Visible = false; gMaingui.Visible = false; gAction.Visible = false;
    cEd.Walk(player.x+60, player.y, eBlock, eWalkableAreas);
    player.Say("Ed?");
    cJeff.FaceDirection(eDirDown);
    player.Say("Was machst du hier?");
    cEd.Say("Ich soll für meine Mamma das Buch von dieser Britney besorgen.");
    cEd.Say("Hier soll es noch jemanden geben, der es verkauft.");
    cEd.Walk(6, 134, eBlock, eWalkableAreas);
    cEd.EnterRoom(25, cTom.x-3, cTom.y+10, eDirUp);
    Wait(10);
    cJeff.FaceDirection(eDirLeft);
    cJeff.Say("Verhandlungen zwischen zwei Zurückgebliebenen. Da würde ich zu gerne mal Mäuschen spielen.");
    cJeff.FaceDirection(eDirDown);
    cBritney.EnterRoom(7, 56, 133, eDirUp);
    ControllMode(true, true);
    Mouse.Visible = true; gMaingui.Visible = true; gAction.Visible = true;    
    EndCutscene();
  }
}

function room_Leave()
{

}

/****************************
*   Tür-Hotspot-Funktionen
****************************/



/****************************
*   Objekt-Funktionen
****************************/



/****************************
*   Hotspot-Funktionen
****************************/
function hEingang_AnyClick()
{
	if ( b_ShackOffen ) // dritte Zeile oben
	{
		if ( any_click_on_door(11, oTuer, 236, 135, eDirUp, 24, 39, 134, eDirRight) == 0 ) Unhandled();	
	}
	else
	if (any_click_move(269, 135, eDirUp))
	{
		if (UsedAction(eActWalkTo))
		{
		//	Wait(5);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("\"Woodie's Pizza Shack\"."))
				player.Say("Der Eingang zu \"Woodie's Pizza Shack\".");
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Warum?"))
				player.Say("Gute Idee, aber es hapert an der Umsetzung.");
		}
		else if (UsedAction(eActUse) || UsedAction(eActOpen))
		{
			Wait(5);
			if ( cAchmed == player )
			{
				player.Say("Zu!");
				Wait (50);
				cJeff.FaceDirection(eDirRight);
				cJeff.Say("Unser Lokal öffnet erst heute abend, Achmed.");
				player.FaceDirection(eDirLeft);
				cJeff.Say("Wenn du 'ne Pizza willst, dann komm' hier rüber.");
				player.Say("Später vielleicht.");
				cJeff.Say("Wie du meinst ...");
				cJeff.FaceDirection(eDirUp);				
			}
			else if ( cSyd == player )
			{
				player.Say("Geschlossen!");
				Wait (50);
				cJeff.FaceDirection(eDirRight);
				cJeff.Say("Unser Lokal öffnet erst heute abend, Syd.");
				player.FaceDirection(eDirLeft);
				cJeff.Say("Wenn du 'ne Pizza willst, dann komm' hier rüber.");
				player.Say("Vielleicht später.");
				cJeff.Say("Wie du meinst ...");
				cJeff.FaceDirection(eDirUp);
			}
			else
			{
				cJeff.FaceDirection(eDirRight);
				cJeff.Say("Unser Lokal öffnet erst heute abend.");
				cJeff.FaceDirection(eDirUp);				
			}
		}
		else if (UsedAction(eActClose) || UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			cJeff.FaceDirection(eDirRight);
			cJeff.Say("Was machst du da?");
			player.FaceDirection(eDirLeft);
			player.Say("Ähh.");
			cJeff.Say("Lass das brav bleiben.");
			Wait(20);
			cJeff.FaceDirection(eDirUp);
		}
		else Unhandled();
	}
}

function hPlakatL_AnyClick()
{
	if (any_click_move(140, 133, eDirUp))
	{
    if (UsedAction(eActLookAt))
    {
      Wait(5);
      player.Say("\"Day of the Dead\"");
      player.Say("Den habe ich kürzlich gesehen.");
      player.Say("Ich mochte den Schurken.");
      if (!dJeff.HasOptionBeenChosen(7)) dJeff.SetOptionState(7, eOptionOn);
    }
    else if (UsedAction(eActUse))
    {
      Wait(5);
      player.Say("Mit was soll ich das Plakat benutzen?");
    }
    else if (UsedAction(eActPickUp) || UsedAction(eActPull))
    {
      Wait(5);
      player.Say("Ich sammel keine Filmplakate.");
      player.Say("Außerdem müsste ich erst die Scheibe einschlagen.");
    }
    else if (UsedAction(eActOpen))
    {
      Wait(5);
      player.Say("Ich bräuchte einen Schlüssel, um den Kasten aufzumachen.");
    }
    else if (UsedAction(eActClose))
    {
      Wait(5);
      player.Say("Der Kasten ist bereits zu.");
    }
    else if (UsedAction(eActPush))
    {
      Wait(5);
      player.Say("Das ergibt keinen Sinn.");
    }
    else Unhandled();
	}
}

function hPlakatR_AnyClick()
{
  if (any_click_move(175, 133, eDirUp))
	{
    if (UsedAction(eActLookAt))
    {
      Wait(5);
      player.Say("Das ist der neue Disneyfilm.");
      player.Say("\"Disneys Voll auf Zak!\"");
      player.Say("Es geht um einen Boulevard-Reporter, der mit seinem treuen Goldfisch und einem trockenen Baquette die Welt vor Außerirdischen rettet.");
    }
    else if (UsedAction(eActUse))
    {
      Wait(5);
      player.Say("Mit was soll ich das Plakat benutzen?");
    }
    else if (UsedAction(eActPickUp) || UsedAction(eActPull))
    {
      Wait(5);
      player.Say("Ich sammel keine Filmplakate.");
      player.Say("Außerdem müsste ich erst die Scheibe einschlagen.");
    }
    else if (UsedAction(eActOpen))
    {
      Wait(5);
      player.Say("Ich bräuchte einen Schlüssel, um den Kasten aufzumachen.");
    }
    else if (UsedAction(eActClose))
    {
      Wait(5);
      player.Say("Der Kasten ist bereits zu.");
    }
    else if (UsedAction(eActPush))
    {
      Wait(5);
      player.Say("Das ergibt keinen Sinn.");
    }
    else Unhandled();
	}
}

function hStrasse_AnyClick()
{
	if (any_click_move(308, 134, eDirRight))
	{
		if (UsedAction(eActWalkTo) || UsedAction(eActUse))
		{
			Wait(5);
			player.ChangeRoom(7, 11, 131);
			player.FaceDirection(eDirRight);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Weg zu S-Mart-Ashlay."))
				player.Say("Da geht's zum S-Mart.");
		}
		else if (UsedAction(eActPickUp) || UsedAction(eActOpen) || 
						 UsedAction(eActClose) || UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Straße sein unkoporzativ.")) // unkooperativ
				player.Say("Das lässt die Straße nicht mit sich machen.");
		}
		else Unhandled();
	}
}

function hBackyard_AnyClick()
{
  if (any_click_move(6, 134, eDirLeft))
	{
		if (UsedAction(eActWalkTo) || UsedAction(eActUse))
		{
			Wait(5);
			player.ChangeRoom(25, 304, 137);
			player.FaceDirection(eDirLeft);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
      player.Say("Da gehts zum Hinterhof.");
		}
		else if (UsedAction(eActPickUp) || UsedAction(eActOpen) || 
						 UsedAction(eActClose) || UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			player.Say("Das lässt die Straße nicht mit sich machen.");
		}
		else Unhandled();
	}
}

/****************************
*   Regionen-Funktionen
****************************/



