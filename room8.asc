// S-Mart (Syd)

/****************************
*   Raum-Funktionen
****************************/
function room_FirstLoad()
{
	player.Walk(390, 132, eBlock, eWalkableAreas);
	// nur, wenn der Spieler über den regulären Weg kommt
	// nutzt er die Debugging-Funktion von einem anderen Raum als der Straße aus,
	// so wird das nicht ausgeführt
	if (player.PreviousRoom == 7)
	{
		if ( cSyd == player )
		{
			player.Say("Hi, Ash!");
			Wait(5);
			cAshley.FaceDirection(eDirRight);
			cAshley.Say("Hi, Syd!");
		}
		else if ( cAchmed == player )
		{
			player.Say("Hi, Ashlay!");
			Wait(5);
			cAshley.FaceDirection(eDirRight);
			cAshley.Say("Hi, Achmed!");
			cAshley.Say("Aber es heißt eigentlich Ashley!");
		}
		else
		{
			player.Say("Hi, Ashley!");
			Wait(5);
			cAshley.FaceDirection(eDirRight);
			cAshley.Say("Hi!");
		}
	}
	Wait(15);
	cAshley.FaceDirection(eDirDown);
}

function room_Load()
{
  aSMart.Play();
  Game.StopAudio(eAudioTypeAmbientSound);
	oTuer.init_object(7);
  
  if (cEd.Room == 25 && player.HasInventory(iHamster)) // The player got the hamster. Now, Ed is moved to the entrance of the S-Mart.
  {
    cEd.ChangeRoom(7, 56, 133);
    cEd.FaceDirection(eDirDown);
    edExpressions(eSad);
  }
}

function room_AfterFadeIn()
{
	cAshley.Walk(212, 129, eBlock, eWalkableAreas);
	Wait(5);
	cAshley.FaceDirection(eDirDown);
}


/****************************
*   Tür-Hotspot-Funktionen
****************************/
function hTuer_AnyClick()
{
	if (any_click_on_door (7, oTuer, 446, 135, eDirRight, 7, 122, 130, eDirDown) == 0 ) Unhandled ();
}


/****************************
*   Objekt-Funktionen
****************************/
function oKettensaege_AnyClick()
{
	if (any_click_move(249, 140, eDirUp))
	{
		if (UsedAction(eActWalkTo))
		{
		//	Wait(5);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			player.Say("Eine Kettensäge.");
			if ( player == cAchmed )
			{
				player.Say("Ashlay können machen gute Sachen damit.");
				cAshley.Say("Es heißt Ashley, Achmed.");
			}
			else
			{
				player.Say("Ash kann echt gut damit umgehen.");
			}
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
      player.Say("Ich benötige keine Kettensäge.");
		}
    else if (UsedAction(eActUse))
		{
			Wait(5);
			player.Say("Die kann ich nicht einfach benutzen.");
      player.FaceDirection(eDirDown);
      player.Say("Das ist ein Laden.");
      player.Say("Ich müsste sie also erst mal kaufen.");
		}
		else if (UsedAction(eActOpen) || UsedAction(eActClose) || 
						 UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Zu weit weg."))
				player.Say("Ich komme nicht ran.");
		}
		else Unhandled();
	}
}

function oGewehr_AnyClick()
{
	if (any_click_move(183, 140, eDirUp))
	{
		if (UsedAction(eActWalkTo))
		{
		//	Wait(5);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			player.Say("Ein Gewehr.");
			if ( !cAchmed.SayIfPlayer("Ashlay können gut mit Gewehr."))
				player.Say("Ash weiß alles über diese Geräte.");

			if ( cAchmed == player )
			{
				cAshley.Say("Es heißt Ashley, Achmed");
			}
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
      player.Say("Ich benötige kein Gewehr.");
		}
    else if (UsedAction(eActUse))
		{
			Wait(5);
			player.Say("Das kann ich nicht einfach benutzen.");
      player.FaceDirection(eDirDown);
      player.Say("Das ist ein Laden.");
      player.Say("Ich müsste es also erst mal kaufen.");
		}
		else if (UsedAction(eActOpen) || UsedAction(eActClose) || 
						 UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			player.Say("Ich komme nicht ran.");
		}
		else Unhandled();
	}
}


/****************************
*   Hotspot-Funktionen
****************************/
function hTelefon_AnyClick()
{
	if (any_click_move(265, 139, eDirUp))
	{
		if (UsedAction(eActWalkTo))
		{
		//	Wait(5);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Das sein elektronische Ohrmuschel."))
				player.Say("Ein Telefon.");
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Ashlay würden benutzen Gewehr mit Achmed."))
				player.Say("Ash hätte etwas dagegen.");
		}
		else if (UsedAction(eActUse))
		{
			Wait(5);
			if ( cAchmed == player )
			{
				player.Say("Achmed haben eigenes Telefon.");
			}
			else if ( cSyd == player )
			{
				player.Say("Ash, kann ich mal telefonieren?");
				cAshley.Say("Sorry, Syd.");
				cAshley.Say("Dafür haben wir den Münzfernsprecher neben der Tür.");
				player.Say("Auch gut.");
			}
			else
			{
				player.Say("Mister Ashley, darf ich mal telefonieren.");
				cAshley.Say("Gerne. Dort hinten an der Wand ist ein Münzfernsprecher.");
				player.Say("Ah, danke.");
			}
		}
		else if (UsedAction(eActOpen) || UsedAction(eActClose) ||
						 UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Ashlay nicht mögen das."))
				player.Say("Ash hätte etwas dagegen.");
				
			if ( cAchmed == player )
			{
				cAshley.Say("Es heißt Ashley, Achmed!");
			}
		}
		else Unhandled();
	}
}

function hSchild_AnyClick()
{
	if (any_click_move(241, 140, eDirUp))
	{
		if (UsedAction(eActWalkTo))
		{
		//	Wait(5);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			player.Say("Hauswaren-Abeilung.");
      player.Say("...");
      cAshley.Say("Kauf smart - kauf im S-Mart!");
		}
		else if (UsedAction(eActPickUp) || UsedAction(eActUse))
		{
			Wait(5);
			if ( cAchmed == player )
			{
				player.Say("Achmed müssen kaufen.");
				player.FaceDirection(eDirDown);
				player.Say("Früher haben Achmed einfach mitgenommen. Aber in Ronville, Achmed das können nicht mehr machen.");
			}
			else
			{
				if (UsedAction(eActPickUp))
					player.Say("Die kann ich nicht einfach nehmen.");
				if (UsedAction(eActUse))
					player.Say("Die kann ich nicht einfach benutzen.");

				player.FaceDirection(eDirDown);
				player.Say("Das ist ein Laden.");
				player.Say("Ich müsste sie also erst mal kaufen.");
			}
		}
		else if (UsedAction(eActOpen) || UsedAction(eActClose) || 
						 UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Ashlay nicht mögen das."))
				player.Say("Ash hätte etwas dagegen.");
				
			if ( cAchmed == player )
			{
				cAshley.Say("Es heißt Ashley, Achmed!");
			}
		}
		else Unhandled();
	}
}

function hFernseher_AnyClick()
{
	if (any_click_move(119, 140, eDirUp))
	{
		if (UsedAction(eActWalkTo))
		{
		//	Wait(5);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			if ( cAchmed == player )
			{
				player.Say("Das sein Seherfern.");
			}
			else
			{
				player.Say("Ich will gar nicht wissen, was da passiert ist.");
			}
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Zu schwer."))
				player.Say("Ash hätte etwas dagegen.");
		}
		else if (UsedAction(eActUse))
		{
			Wait(5);
			if ( cAchmed == player )
			{
				player.Say("Achmed nix wollen schauen fern.");
			}
			else if ( cSyd == player )
			{
        player.Say("Der funktioniert garantiert nicht mehr.");
			}
			else
			{
				player.Say("Ich kann ihn nicht anmachen.");
			}
		}
		else if (UsedAction(eActOpen) || UsedAction(eActClose) || 
						 UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Würden Ashlay, Achmed nie erlauben."))
				player.Say("Ash hätte etwas dagegen.");
		}
	  else if (UsedAction (eActUseInventory))
		{
			if ( iFernbedienung == player.ActiveInventory )
			{
				player.Say("Meine Fernbedienung wird hier nicht funktionieren.");
			} else Unhandled();
		}
		else Unhandled();
	}
}

function hBildschirm_AnyClick()
{
	if (any_click_move(29, 131, eDirUp))
	{
		if (UsedAction(eActWalkTo))
		{
		//	Wait(5);
		}
		else if (UsedAction(eActLookAt) || UsedAction(eActUse))
		{
			Wait(5);
			player.Say("Mal sehen ...");
      Wait(5);
      player.ChangeRoom(9);
      StopMusic();
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Sein Achmed zu schwer."))
				player.Say("Der Automat ist mir zu schwer.");
		}
		else if (UsedAction(eActOpen) || UsedAction(eActClose) || 
						 UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Würde Achmed nie erlauben, Ashley."))
				player.Say("Ash hätte etwas dagegen.");
		}
		else Unhandled();
	}
}

function hFernsprecher_AnyClick()
{
	if (any_click_move(467, 141, eDirRight))
	{
		if (UsedAction(eActWalkTo))
		{
		//	Wait(5);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			if ( cAchmed == player )
			{
				player.Say("Sein Ohrmuschel-Münze.");
			}
			else
			{
				player.Say("'Payphone' klingt eigentlich besser.");
				player.Say("...");
				player.Say("Egal.");
			}
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			if ( cAchmed == player )
			{
				player.Say("Gut.");
				Wait(5);
				cAshley.FaceDirection(eDirRight);
				Wait(5);
				cAshley.Say("Laß den bloß hängen Achmed.");
				Wait(20);
				player.Say("Schade.");
				Wait(5);
				player.FaceDirection(eDirDown);
				Wait(5);
				player.Say("Echt Schade.");
			}
			else
			{
				player.Say("Obwohl sicher Kleingeld im Fernsprecher ist, lasse ich ihn lieber hängen.");
				player.Say("Ash beobachtet mich nämlich.");
			}
		}
		else if (UsedAction(eActUse))
		{
			Wait(5);
			if ( cAchmed == player )
			{
				player.Say("Achmed selber haben Ohrmuschel.");
			}
			else
			{
				player.Say("Ich hab' kein Kleingeld dabei ...");
				player.FaceDirection(eDirDown);
				player.Say("Und ich hab' ein eigenes Telefon in der Wohnung.");
			}
		}
		else if (UsedAction(eActOpen) || UsedAction(eActClose) || 
						 UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Ashlay würden Achmed nie erlauben."))
				player.Say("Ash hätte etwas dagegen.");
		}
		else Unhandled();
	}
}

function oColor_AnyClick()
{
  if (any_click_move(340, 130, eDirUp))
  {
    if (UsedAction(eActLookAt))
    {
      Wait(5);
      player.Say("Ein Eimer lila Farbe.");
    }
    else if (UsedAction(eActUse) || UsedAction(eActOpen))
    {
      Wait(5);
      player.Say("Dafür müsste ich ihn erstmal kaufen.");
    }
    else if (UsedAction(eActPull) || UsedAction(eActPush))
    {
      Wait(5);
      player.Say("Den Eimer hin und her zu bewegen bringt auch nichts.");
    }
    else if (UsedAction(eActClose))
    {
      Wait(5);
      player.Say("Der ist schon zu.");
    }
    else if (UsedAction(eActPickUp))
    {
      Wait(5);
      PlaySound(4);
      player.AddInventory(iFarbeimer);
      oColor.Visible = false;
      Wait(5);
      player.Walk(210, 137, eBlock, eWalkableAreas);
      player.FaceDirection(eDirUp);
      player.Say("Ich würde das hier gerne kaufen.");
      cAshley.Say("Hast du Geld dabei?");
      if (player.HasInventory(iPortemonnaie))
      {
        player.Say("Nein, aber ich zahle per CashCard.");
        cAshley.Say("Auch gut.");
        Wait(5);
        if (!player.HasInventory(iCashCard))
        {
          aSound37.Play();
          Wait(20);
          aSound4.Play();
          player.AddInventory(iCashCard);
          Wait(30);
        }
        player.LoseInventory(iCashCard);
        Wait(5);
        int x_ash = cAshley.x;
        cAshley.Walk(286, cAshley.y, eBlock, eWalkableAreas);
        cAshley.FaceDirection(eDirDown);
        Wait(20);
        cAshley.Walk(x_ash, cAshley.y, eBlock, eWalkableAreas);
        cAshley.FaceDirection(eDirDown);
        Wait(5);
        PlaySound(4);
        player.AddInventory(iCashCard);
        cAshley.Say("Vielen Dank für ihren Einkauf im S-Mart.");
      }
      else
      {
        player.Say("Nein, ich bin komplett blank.");
        cAshley.Say("Dann stell den Eimer bitte wieder zurück.");
        Wait(5);
        player.Walk(340, 130, eBlock, eWalkableAreas);
        player.FaceDirection(eDirUp);
        player.LoseInventory(iFarbeimer);
        oColor.Visible = true;
      }
    }
    else Unhandled();
  }
}

function oWares_AnyClick()
{
  if (any_click_move(317, 125, eDirUp))
  {
    if (UsedAction(eActLookAt))
    {
      Wait(5);
      player.Say("Verschiedene Waren.");
    }
    else if (UsedAction(eActUse))
    {
      Wait(5);
      player.Say("Was davon soll ich benutzen?");
    }
    else if (UsedAction(eActPickUp))
    {
      Wait(5);
      player.Say("Ich brauche nichts davon.");
    }
    else if (UsedAction(eActOpen))
    {
      Wait(5);
      player.Say("Ich kann hier doch nicht einfach irgendwelche Waren öffnen.");
    }
    else if (UsedAction(eActClose))
    {
      Wait(5);
      player.Say("Das ergibt keinen Sinn.");
    }
    else if (UsedAction(eActPull) || UsedAction(eActPush))
    {
      Wait(5);
      player.Say("Ich möchte da nichts bewegen.");
    }
    else Unhandled();
  }
}

/****************************
*   Regionen-Funktionen
****************************/

